#version 330 core
out vec4 FragColor;

struct Material
{
    sampler2D diffuse;
    sampler2D specular;
    sampler2D emission;
    float shininess;
};

struct DirLight
{
    vec3 direction;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct PointLight
{
    vec3 position;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

struct SpotLight
{
    vec3 position;
    vec3 direction;
    float cutOff;
    float outerCutOff;

    float constant;
    float linear;
    float quadratic;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

#define NR_POINT_LIGHTS 4

in vec3 FragPos;
in vec3 Normal;
in vec2 TexCoords;

uniform vec3 viewPos;
uniform DirLight dirLight;
uniform PointLight pointLights[NR_POINT_LIGHTS];
uniform SpotLight spotLight;
uniform Material material;

vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir)
{
    //ë¹›ë°©í–¥ ì •ê·œí™”
    vec3 lightDir = normalize(-light.direction);
    //ë‚œë°˜ì‚¬
    float diff = max(dot(normal, lightDir), 0.0);
    //ë°˜ì‚¬ ë°©í–¥
    vec3 reflectDir = reflect(-lightDir, normal);
    //ì •ë°˜ì‚¬(ë¹› ë°˜í–¥ì— ë”°ë¼ ê±°ìš¸ì²˜ëŸ¼ í•˜ì´ë¼ì´íŠ¸ ë˜ëŠ” ë¶€ë¶„)
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    //ì„¤ì •
    vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));
    vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
    vec3 specular = light.specular * spec * vec3(texture(material.specular, TexCoords));
    return (ambient + diffuse + specular);
}

vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    //ë¹› ë°©í–¥ êµ¬í•˜ê¸°
    vec3 lightDir = normalize(light.position - fragPos);
    //ë²•ì„ , ë¹›ë°©í–¥ ë‚´ì  êµ¬í•˜ê¸°(ë‚œë°˜ì‚¬)
    float diff = max(dot(normal, lightDir), 0.0);
    //ë²•ì„ , ë¹›ë°©í–¥ ìœ¼ë¡œ ë°˜ì‚¬ ë°©í–¥ êµ¬í•˜ê¸° = reflect: -lightDir -2*(dot(normal, -lightDir))*normal
    vec3 reflectDir = reflect(-lightDir, normal);
    //ë°˜ì‚¬ê°’ shininessëŠ” specular ë°˜ì‚¬ê´‘ì˜ ì„¸ê¸°ë¥¼ ì •í•¨ dotì€ ì‹œì•¼ë°©í–¥(akaì¹´ë©”ë¼forward)ê³¼ ë°˜ì‚¬ë°©í–¥ì˜ ë‚´ì ìœ¼ë¡œ ì¼ì¹˜ë„ë¥¼ êµ¬í•˜ê¸° ìœ„í•¨
    //maxëŠ” cosê°’ì„ 0~1ë¡œ ì œí•œí•˜ê¸° ìœ„í•¨
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    //ê±°ë¦¬
    float distance = length(light.position - fragPos);
    //ê±°ë¦¬ì— ë”°ë¥¸ ë¹›ì„¸ê¸° êµ¬í•˜ê¸°
    float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));

    vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));
    vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
    vec3 specular = light.specular * spec * vec3(texture(material.specular, TexCoords));

    ambient *= attenuation;
    diffuse *= attenuation;
    specular *= attenuation;
    return (ambient + diffuse + specular);
}

vec3 CalcSpotLight(SpotLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    vec3 lightDir = normalize(light.position - fragPos);
    
    float diff = max(dot(normal, lightDir), 0.0);
    
    vec3 reflectDir = reflect(-lightDir, normal);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    
    float distance = length(light.position - fragPos);
    float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));    
    //ìŠ¤íŒŸë¼ì´íŠ¸ì˜ ë¹›ì˜ ë°©í–¥ ë²•ì„ ë°±í„°ì˜ cosğœ­
    float theta = dot(lightDir, normalize(-light.direction)); 
    //ë³´ê°„ ê±°ë¦¬
    float epsilon = light.cutOff - light.outerCutOff;
    //0~1ë¡œ ì ì§„ì ìœ¼ë¡œ ë³´ê°„ (theta - light.outerCutOff) / epsilonì€ ë³´ê°„ êµ¬ê°„ì„ êµ¬í•˜ëŠ” ê²ƒ.
    float intensity = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);
    
    vec3 ambient = light.ambient * vec3(texture(material.diffuse, TexCoords));
    vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, TexCoords));
    vec3 specular = light.specular * spec * vec3(texture(material.specular, TexCoords));
    ambient *= attenuation * intensity;
    diffuse *= attenuation * intensity;
    specular *= attenuation * intensity;
    return (ambient + diffuse + specular);
}

void main()
{   
    //ê¸°ë³¸ ì„¤ì • 
    //ë²•ì„ 
    vec3 norm = normalize(Normal);
    //ë°©í–¥
    vec3 viewDir = normalize(viewPos - FragPos);


    vec3 emission = texture(material.emission, TexCoords).rgb;
    //ë°©í–¥ê´‘
    vec3 result = CalcDirLight(dirLight, norm, viewDir);
    //í¬ì¸íŠ¸ë¼ì´íŠ¸(ë³µìˆ˜ ì²˜ë¦¬)
    for(int i = 0; i < NR_POINT_LIGHTS; i++)
        result += CalcPointLight(pointLights[i], norm, FragPos, viewDir);    
    //ìŠ¤íŒŸë¼ì´íŠ¸
    result += CalcSpotLight(spotLight, norm, FragPos, viewDir);    
    result += (emission * 0.4f);
    FragColor = vec4(result, 1.0);
}